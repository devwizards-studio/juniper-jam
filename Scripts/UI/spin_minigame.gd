extends Control

@onready var center: TextureRect = $Center

@onready var indication_label: Label = $IndicationLabel

@onready var required_score_bar: TextureProgressBar = $RequiredScoreBar
@onready var bonus_score_bar: TextureProgressBar = $BonusScoreBar
@onready var timer_bar: TextureProgressBar = $TimerBar

@export var base_required_rotation: float = 30.0
@export var required_rotation_multiplier: float = 5.0

var level: int = 1
var total_required_rotation: float = 30.0

@export var duration := 5.0
@export var keys_list: Array[Key] = [KEY_E, KEY_Q]

var required_orientation: int = 1
var required_key: Key = KEY_E

var elapsed := 0.0
var total_rotation := 0.0
var previous_angle := 0.0
var center_global_position: Vector2 = Vector2.ZERO

#rotation to scored_points conversion method not decided yet
signal minigame_over
signal minigame_won(scored_points: float)
signal minigame_lost

func _ready():
	generate_random()
	total_required_rotation = base_required_rotation + level * required_rotation_multiplier
	required_score_bar.max_value = total_required_rotation
	timer_bar.max_value = duration
	timer_bar.value = duration
	
	center.flip_h = required_orientation < 0
	
	indication_label.text = "Hold " + OS.get_keycode_string(required_key) + "
	 + 
	spin the cursor"
	
	center_global_position = center.global_position + center.pivot_offset
	
	previous_angle = (
		get_global_mouse_position() - center_global_position
	).angle()

func generate_random():
	required_key = keys_list.pick_random()
	required_orientation = [-1,1].pick_random()         #-1 = Counter-Clockwise, 1 = Clockwise

func _process(delta):
	elapsed += delta
	timer_bar.value = duration - elapsed

	var angle = (
		get_global_mouse_position() - center_global_position
	).angle()

	var delta_angle = wrapf(
		angle - previous_angle,
		-PI,
		PI
	)
	
	#delta_angle = clamp(delta_angle, -0.8, 0.8)

	#if orientation = -1, delta_angle needs to be negative
	#if orientation = 1, delta_angle needts to be positive
	if Input.is_key_pressed(required_key) and required_orientation * delta_angle > 0.0 and abs(delta_angle) > 0.1:
		total_rotation += abs(delta_angle)
		required_score_bar.value = total_rotation
		bonus_score_bar.value = total_rotation - total_required_rotation
		center.rotation += delta_angle

	previous_angle = angle

	if elapsed >= duration:
		if total_rotation >= total_required_rotation:
			spinner_won()
		else:
			spinner_failed()
		minigame_over.emit()
		queue_free()

func spinner_won():
	minigame_won.emit(total_rotation)

func spinner_failed():
	minigame_lost.emit()
